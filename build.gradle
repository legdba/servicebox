apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'maven'

ext.revision = 'git rev-list --count HEAD'.execute().text.trim()
ext.hash = 'git rev-parse --short HEAD'.execute().text.trim()
ext.branch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
group = 'com.brimarx'
version = "r${revision}-${hash}-${ext.branch}"
description = """REST services for demo and test purpose."""
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Gradle Wrapper for predictible builds
// See http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

repositories {
    mavenCentral()
}

dependencies {
    compile     'org.slf4j:slf4j-api:1.7.+',
                'ch.qos.logback:logback-classic:1.1.+',
                'net.logstash.logback:logstash-logback-encoder:4.5.+',
                'io.swagger:swagger-jersey2-jaxrs:1.5.+',
                'org.glassfish.jersey.core:jersey-server:2.+',
                'org.glassfish.jersey.containers:jersey-container-servlet:2.+',
                'org.glassfish.jersey.media:jersey-media-json-jackson:2.+',
                'org.eclipse.jetty:jetty-server:9.3.3.v20150827',
                'org.eclipse.jetty:jetty-servlet:9.3.3.v20150827',
                'com.beust:jcommander:1.+',
                'com.datastax.cassandra:cassandra-driver-core:2.2.+',
                'biz.paluch.redis:lettuce:4.0+',
                'org.glassfish.jersey.core:jersey-client:2.+' // for JAX-RS client

    testCompile 'org.testng:testng:6.+',
                'org.uncommons:reportng:1.+'
}

// ensure that test resources (src/test/resources) are added to CLASSPATH;
// see http://forums.gradle.org/gradle/topics/tests_arent_executed_when_setting_the_test_runtimeclasspath and
// http://gradle.org/docs/current/dsl/org.gradle.api.tasks.testing.Test.html#org.gradle.api.tasks.testing.Test:classpath
sourceSets {
  main {
    runtimeClasspath = files(output.resourcesDir) + runtimeClasspath
  }
  test {
    runtimeClasspath = files(output.resourcesDir) + runtimeClasspath
  }
}

test {
  useTestNG() {
    suiteXmlBuilder().suite(name: 'gradle-testng-mockito-bootstrap', parallel: 'tests') {
      test (name : 'all-tests') {
        packages {
          'package' (name: 'com.brimarx.*')
        }
      }
    }
  }
  options {
    listeners << 'org.uncommons.reportng.HTMLReporter'
    listeners << 'org.uncommons.reportng.JUnitXMLReporter'
  }
  ext.useDefaultListeners = true
  ext.workingDirectory = 'build/'
}

mainClassName = 'com.brimarx.servicebox.EmbededServer'
//mainClassName = 'com.brimarx.servicebox.DemoClient'

task buildInfo() {
    println "version : " + version
    println "appname : " + project.name
    println "disttar : " + project.name + '-' + version + '.tar'
}
